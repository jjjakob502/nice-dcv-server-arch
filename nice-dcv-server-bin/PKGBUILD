pkgname=nice-dcv-server-bin
_pkgname=nice-dcv-server
_majver=2025.0
_minver=20103
pkgver=${_majver}.${_minver}
pkgrel=1
pkgdesc="NICE DCV server for remote desktop and application streaming"
arch=('x86_64')
url="https://www.nice-software.com/products/dcv"
license=('custom: commercial')
depends=('brotli' 'cairo' 'cracklib' 'cups' 'libdrm' 'elfutils' 'libepoxy'
         'gcc-libs' 'gdk-pixbuf2' 'krb5' 'gst-plugins-base-libs' 'gtk3'
         'systemd-libs' 'keyutils' 'pam' 'libpulse' 'sqlite' 'openssl'
         'libunwind' 'libvdpau' 'libx11' 'libxcb' 'libxext' 'libxi'
         'dconf' 'dbus' 'libselinux')
optdepends=('nice-xdcv: for virtual session support')
install="${pkgname}.install"
source=("nice-dcv-ubuntu2404-x86_64-${_majver}-${_minver}.tgz::https://d1uj6qtbmh3dt5.cloudfront.net/nice-dcv-ubuntu2404-x86_64.tgz")
sha256sums=('a39374d39f2d849bd13ee101970bb9eea15a8c5ec743799b7cbb7f562ece9e17')

package() {
  cd "${srcdir}/nice-dcv-${_majver}-${_minver}-ubuntu2404-x86_64"

  # Extract the server deb package
  local _debfile="${_pkgname}_${pkgver}-1_amd64.ubuntu2404.deb"
  bsdtar -xf "${_debfile}"
  bsdtar -xf data.tar.zst

  # Copy files to package directory
  cp -r etc usr "${pkgdir}/"

  # Install license
  install -Dm644 "${pkgdir}/usr/share/dcv/license/EULA.txt" \
    "${pkgdir}/usr/share/licenses/${_pkgname}/EULA.txt"
}